// CRC32: 95661BB6 | Matched 156 variants of 1 shaders: Hidden/Internal-PrePassLighting
// 
//                                          Unity headers extracted from Internal-PrePassLighting.shader
//                                          Shader "Hidden/Internal-PrePassLighting" {
//                                            Properties {
//                                             _LightTexture0 ("", any) = "" { }
//                                             _LightTextureB0 ("", 2D) = "" { }
//                                             _ShadowMapTexture ("", any) = "" { }
//                                            }
//                                            Fallback Off
//                                            SubShader 1/1 {
// 00000000000000000000000000fffffffffffff:     Pass 1/3 {
// 0000000000000fffffffffffff0000000000000:     Pass 2/3 {
// fffffffffffff00000000000000000000000000:     Pass 3/3 {
//                                                Tags { "SHADOWSUPPORT"="true" }
//                                                ZWrite Off
//                                                Fog { Mode Off }
// 00000000000000000000000000fffffffffffff:       Blend DstColor Zero
// ffffffffffffffffffffffffff0000000000000:       Blend One One
// 00000000000000000000000000fffffffffffff:       GpuProgramID 1343
// fffffffffffff00000000000000000000000000:       GpuProgramID 189300
// 0000000000000fffffffffffff0000000000000:       GpuProgramID 95640
//                                                Program "vp" {
//                                                  SubProgram "d3d9 " {
// 000000000000200000000000020000000000002:           Keywords { "DIRECTIONAL" "SHADOWS_OFF" "SUNSHINE_DISABLED" }
// 001000000000000100000000000010000000000:           Keywords { "DIRECTIONAL" "SHADOWS_OFF" "SUNSHINE_FILTER_HARD" }
// 000000800000000000080000000000008000000:           Keywords { "DIRECTIONAL" "SHADOWS_OFF" "SUNSHINE_FILTER_PCF_2x2" }
// 000000000400000000000040000000000004000:           Keywords { "DIRECTIONAL" "SHADOWS_OFF" "SUNSHINE_FILTER_PCF_3x3" }
// 000000000004000000000000400000000000040:           Keywords { "DIRECTIONAL" "SHADOWS_SCREEN" "SUNSHINE_DISABLED" }
// 020000000000002000000000000200000000000:           Keywords { "DIRECTIONAL" "SHADOWS_SCREEN" "SUNSHINE_FILTER_HARD" }
// 000010000000000001000000000000100000000:           Keywords { "DIRECTIONAL" "SHADOWS_SCREEN" "SUNSHINE_FILTER_PCF_2x2" }
// 000000008000000000000800000000000080000:           Keywords { "DIRECTIONAL" "SHADOWS_SCREEN" "SUNSHINE_FILTER_PCF_3x3" }
// 000000000001000000000000100000000000010:           Keywords { "DIRECTIONAL_COOKIE" "SHADOWS_OFF" "SUNSHINE_DISABLED" }
// 008000000000000800000000000080000000000:           Keywords { "DIRECTIONAL_COOKIE" "SHADOWS_OFF" "SUNSHINE_FILTER_HARD" }
// 000004000000000000400000000000040000000:           Keywords { "DIRECTIONAL_COOKIE" "SHADOWS_OFF" "SUNSHINE_FILTER_PCF_2x2" }
// 000000002000000000000200000000000020000:           Keywords { "DIRECTIONAL_COOKIE" "SHADOWS_OFF" "SUNSHINE_FILTER_PCF_3x3" }
// 000000000008000000000000800000000000080:           Keywords { "DIRECTIONAL_COOKIE" "SHADOWS_SCREEN" "SUNSHINE_DISABLED" }
// 040000000000004000000000000400000000000:           Keywords { "DIRECTIONAL_COOKIE" "SHADOWS_SCREEN" "SUNSHINE_FILTER_HARD" }
// 000020000000000002000000000000200000000:           Keywords { "DIRECTIONAL_COOKIE" "SHADOWS_SCREEN" "SUNSHINE_FILTER_PCF_2x2" }
// 000000010000000000001000000000000100000:           Keywords { "DIRECTIONAL_COOKIE" "SHADOWS_SCREEN" "SUNSHINE_FILTER_PCF_3x3" }
// 000000000080000000000008000000000000800:           Keywords { "POINT" "SHADOWS_CUBE" "SHADOWS_SOFT" "SUNSHINE_DISABLED" }
// 400000000000040000000000004000000000000:           Keywords { "POINT" "SHADOWS_CUBE" "SHADOWS_SOFT" "SUNSHINE_FILTER_HARD" }
// 000200000000000020000000000002000000000:           Keywords { "POINT" "SHADOWS_CUBE" "SHADOWS_SOFT" "SUNSHINE_FILTER_PCF_2x2" }
// 000000100000000000010000000000001000000:           Keywords { "POINT" "SHADOWS_CUBE" "SHADOWS_SOFT" "SUNSHINE_FILTER_PCF_3x3" }
// 000000000010000000000001000000000000100:           Keywords { "POINT" "SHADOWS_CUBE" "SUNSHINE_DISABLED" }
// 080000000000008000000000000800000000000:           Keywords { "POINT" "SHADOWS_CUBE" "SUNSHINE_FILTER_HARD" }
// 000040000000000004000000000000400000000:           Keywords { "POINT" "SHADOWS_CUBE" "SUNSHINE_FILTER_PCF_2x2" }
// 000000020000000000002000000000000200000:           Keywords { "POINT" "SHADOWS_CUBE" "SUNSHINE_FILTER_PCF_3x3" }
// 000000000000100000000000010000000000001:           Keywords { "POINT" "SHADOWS_OFF" "SUNSHINE_DISABLED" }
// 000800000000000080000000000008000000000:           Keywords { "POINT" "SHADOWS_OFF" "SUNSHINE_FILTER_HARD" }
// 000000400000000000040000000000004000000:           Keywords { "POINT" "SHADOWS_OFF" "SUNSHINE_FILTER_PCF_2x2" }
// 000000000200000000000020000000000002000:           Keywords { "POINT" "SHADOWS_OFF" "SUNSHINE_FILTER_PCF_3x3" }
// 000000000100000000000010000000000001000:           Keywords { "POINT_COOKIE" "SHADOWS_CUBE" "SHADOWS_SOFT" "SUNSHINE_DISABLED" }
// 800000000000080000000000008000000000000:           Keywords { "POINT_COOKIE" "SHADOWS_CUBE" "SHADOWS_SOFT" "SUNSHINE_FILTER_HARD" }
// 000400000000000040000000000004000000000:           Keywords { "POINT_COOKIE" "SHADOWS_CUBE" "SHADOWS_SOFT" "SUNSHINE_FILTER_PCF_2x2" }
// 000000200000000000020000000000002000000:           Keywords { "POINT_COOKIE" "SHADOWS_CUBE" "SHADOWS_SOFT" "SUNSHINE_FILTER_PCF_3x3" }
// 000000000020000000000002000000000000200:           Keywords { "POINT_COOKIE" "SHADOWS_CUBE" "SUNSHINE_DISABLED" }
// 100000000000010000000000001000000000000:           Keywords { "POINT_COOKIE" "SHADOWS_CUBE" "SUNSHINE_FILTER_HARD" }
// 000080000000000008000000000000800000000:           Keywords { "POINT_COOKIE" "SHADOWS_CUBE" "SUNSHINE_FILTER_PCF_2x2" }
// 000000040000000000004000000000000400000:           Keywords { "POINT_COOKIE" "SHADOWS_CUBE" "SUNSHINE_FILTER_PCF_3x3" }
// 000000000000800000000000080000000000008:           Keywords { "POINT_COOKIE" "SHADOWS_OFF" "SUNSHINE_DISABLED" }
// 004000000000000400000000000040000000000:           Keywords { "POINT_COOKIE" "SHADOWS_OFF" "SUNSHINE_FILTER_HARD" }
// 000002000000000000200000000000020000000:           Keywords { "POINT_COOKIE" "SHADOWS_OFF" "SUNSHINE_FILTER_PCF_2x2" }
// 000000001000000000000100000000000010000:           Keywords { "POINT_COOKIE" "SHADOWS_OFF" "SUNSHINE_FILTER_PCF_3x3" }
// 000000000040000000000004000000000000400:           Keywords { "SHADOWS_DEPTH" "SHADOWS_NATIVE" "SHADOWS_SOFT" "SPOT" "SUNSHINE_DISABLED" }
// 200000000000020000000000002000000000000:           Keywords { "SHADOWS_DEPTH" "SHADOWS_NATIVE" "SHADOWS_SOFT" "SPOT" "SUNSHINE_FILTER_HARD" }
// 000100000000000010000000000001000000000:           Keywords { "SHADOWS_DEPTH" "SHADOWS_NATIVE" "SHADOWS_SOFT" "SPOT" "SUNSHINE_FILTER_PCF_2x2" }
// 000000080000000000008000000000000800000:           Keywords { "SHADOWS_DEPTH" "SHADOWS_NATIVE" "SHADOWS_SOFT" "SPOT" "SUNSHINE_FILTER_PCF_3x3" }
// 000000000002000000000000200000000000020:           Keywords { "SHADOWS_DEPTH" "SHADOWS_NATIVE" "SPOT" "SUNSHINE_DISABLED" }
// 010000000000001000000000000100000000000:           Keywords { "SHADOWS_DEPTH" "SHADOWS_NATIVE" "SPOT" "SUNSHINE_FILTER_HARD" }
// 000008000000000000800000000000080000000:           Keywords { "SHADOWS_DEPTH" "SHADOWS_NATIVE" "SPOT" "SUNSHINE_FILTER_PCF_2x2" }
// 000000004000000000000400000000000040000:           Keywords { "SHADOWS_DEPTH" "SHADOWS_NATIVE" "SPOT" "SUNSHINE_FILTER_PCF_3x3" }
// 000000000000400000000000040000000000004:           Keywords { "SHADOWS_OFF" "SPOT" "SUNSHINE_DISABLED" }
// 002000000000000200000000000020000000000:           Keywords { "SHADOWS_OFF" "SPOT" "SUNSHINE_FILTER_HARD" }
// 000001000000000000100000000000010000000:           Keywords { "SHADOWS_OFF" "SPOT" "SUNSHINE_FILTER_PCF_2x2" }
// 000000000800000000000080000000000008000:           Keywords { "SHADOWS_OFF" "SPOT" "SUNSHINE_FILTER_PCF_3x3" }
//                                                    Bind "vertex" Vertex
//                                                    Bind "normal" Normal
//                                                    Matrix 4 [glstate_matrix_modelview0] 3
//                                                    Matrix 0 [glstate_matrix_mvp]
//                                                    Float 9 [_LightAsQuad]
//                                                    Vector 7 [_ProjectionParams]
//                                                    Vector 8 [_ScreenParams]
//                                                  }
//                                                }
//                                              }
//                                            }
//                                          }
// 
// Headers extracted with DarkStarSword's extract_unity_shaders.py
// https://raw.githubusercontent.com/DarkStarSword/3d-fixes/master/extract_unity_shaders.py

    vs_3_0
    def c10, 0.5, -1, 1, -999
    dcl_position v0
    dcl_normal v1
    dcl_position o0
    dcl_texcoord o1
    dcl_texcoord1 o2.xyz

// Extra output to pass the FOV to pixel shaders:
dcl_texcoord5 o5.x

def c220, 0, 1, 0.0625, 1
dcl_2d s0

    // Rearranged to simplify shader:
    dp4 r0.x, c0, v0
    dp4 r0.y, c1, v0
    dp4 r0.z, c2, v0
    dp4 r0.w, c3, v0
    mov o0, r0 // moved up before the stereo correction

// Determine if drawing a full screen quad by testing if output position w == 1.
// Seems to be more reliable than an input position test:
if_eq r0.w, c220.y
	mov r23.x, c220.y // Set flag to indicate we are drawing a full-screen quad

	// Our projection matrix will not be valid, so we use the matrices
	// copied from a different shader:
	mov r25, c180 // MV[0]
	mov r26, c181 // MV[1]
	mov r27, c182 // MV[2]
	mov r28, c190 // MVP[0]
else
	mov r23.x, c220.x // Clear flag to indicate we are drawing a light in the scene

	// Our projection matrix will be valid, so we can use the matrices
	// passed into this shader:
	mov r25, c4 // MV[0]
	mov r26, c5 // MV[1]
	mov r27, c6 // MV[2]
	mov r28, c0 // MVP[0]
endif

mov r24, c200
if_eq r24.w, c220.y
	// Use inverse projection matrix is valid, so use it directly:
	mov r30.x, c170.x
else
	// We can't use Helix mod to invert the MV matrix since we are either
	// using the matrix passed directily into this shader, or a 3x4 matrix
	// copied from a different shader (Helix mod expects a 4x4 matrix).
	// So let's do it directly in assembly instead! It sounds complicated,
	// but we can make some large simplifications based on assumptions
	// about the MV and MVP matrices and the fact we only need one cell of
	// the (inverse) projection matrix, which means it is actually possible
	// to do in a farily small number of instructions :)

	// 1. Calculate 1/determinant of the MV matrix, simplifying by assuming the
	//    4th column of the MV matrix is 0,0,0,1
	//
	// mathomatic simplified it to:
	// 1 / ((m12*((m20*m01) - (m21*m00))) + (m02*((m21*m10) - (m20*m11))) + (m22*((m00*m11) - (m01*m10))));
	//
	// Replace row numbers with register components (assumes column-major order):
	//   (mv2.x*((mv0.y*mv1.z) - (mv0.z*mv1.y)))
	// + (mv2.y*((mv0.z*mv1.x) - (mv0.x*mv1.z)))
	// + (mv2.z*((mv0.x*mv1.y) - (mv0.y*mv1.x)))

	// Do some multiplications in parallel with SIMD instructions:
	mul r20.xyz, r25.yzx, r26.zxy	// mv0.y*mv1.z, mv0.z*mv1.x, mv0.x*mv1.y
	mul r21.xyz, r25.zxy, r26.yzx	// mv0.z*mv1.y, mv0.x*mv1.z, mv0.y*mv1.x
	// Do the subtractions:
	add r20.xyz, r20.xyz, -r21.xyz	// mv0.y*mv1.z - mv0.z*mv1.y, mv0.z*mv1.x - mv0.x*mv1.z, mv0.x*mv1.y - mv0.y*mv1.x
	// Now the multiplications:
	mul r20.xyz, r20.xyz, r27.xyz
	// Sum it together to get the determinant:
	add r22.w, r20.x, r20.y
	add r22.w, r22.w, r20.z
	if_eq r22.w, c220.x
		mov r30.x, c220.w // One of the inputs was zero, use a hardcoded FOV
	else
		// And finally get 1/determinant:
		rcp r22.w, r22.w

		// 2. Calculate the 1st row of the inverted MV matrix, simplifying by assuimg
		//    the 4th column of the MV matrix is 0,0,0,1
		//
		// m00 = (mv1.y*mv2.z - mv1.z*mv2.y) / determinant
		// m01 = (mv1.z*mv2.x - mv1.x*mv2.z) / determinant
		// m02 = (mv1.x*mv2.y - mv1.y*mv2.x) / determinant

		// Do some multiplications in parallel with SIMD instructions:
		mul r20.xyz, r26.yzx, r27.zxy	// mv1.y*mv2.z, mv1.z*mv2.x, mv1.x*mv2.y
		mul r21.xyz, r26.zxy, r27.yzx	// mv1.z*mv2.y, mv1.x*mv2.z, mv1.y*mv2.x
		// Do the subtractions:
		add r20.xyz, r20.xyz, -r21.xyz	// mv1.y*mv2.z - mv1.z*mv2.y, mv1.z*mv2.x - mv1.x*mv2.z, mv1.x*mv2.y - mv1.y*mv2.x
		// Multiply against 1/determinant:
		mul r20.xyz, r20.xyz, r22.www

		// 3. Multiply the first row of the inverted MV matrix with the 1st column of
		//    the MVP matrix (MV.I[0,3] is 0, so only worry about the 1st three):
		dp3 r30.x, r20.xyz, r28.xyz

		if_eq r30.x, c220.x
			mov r30.x, c220.w // One of the inputs was zero, use a hardcoded FOV
		else
			rcp r30.x, r30.x // Calculate P.I[0,0]
		endif
	endif
endif

mov o5.x, r30.x // Copy P.I[0,0] to pixel shaders

if_eq r23.x, c220.x
	// Apply stereo correction to screen coords stored in texcoord:
	texldl r31, c220.z, s0
	add r31.w, r0.w, -r31.y
	mul r31.w, r31.w, r31.x
	add r0.x, r0.x, r31.w
endif

    mul r1.x, r0.y, c7.x
    mul r1.w, r1.x, c10.x
    mul r1.xz, r0.xyww, c10.x
    mad o1.xy, r1.z, c8.zwzw, r1.xwzw
    mov r1.w, c10.w
    slt r1.x, r1.w, c9.x
    slt r1.y, -v1_abs.z, v1_abs.z
    lrp r2.x, r1.x, c9.x, r1.y
    dp4 r1.x, c4, v0
    dp4 r1.y, c5, v0
    dp4 r1.z, c6, v0

if_eq r23.x, c220.x
	// Apply view-space stereo correction to ray:
	mad r1.x, r31.w, r30.x, r1.x
endif

    mul r2.yzw, r1.xxyz, c10.xyyz
    mad r1.xyz, r1, -c10.yyzw, v1
    mad o2.xyz, r2.x, r1, r2.yzww
    mov o1.zw, r0
