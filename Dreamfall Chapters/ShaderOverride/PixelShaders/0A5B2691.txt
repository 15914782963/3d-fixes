// Shader replaced with Unity original Internal-PrePassCollectShadows and inputs remaped

//- CRC32: 0A5B2691 | Unity headers extracted from Internal-PrePassCollectShadows.shader
//+ CRC32: 871F16C5 | Unity headers extracted from Internal-PrePassCollectShadows.shader
//  Shader "Hidden/Internal-PrePassCollectShadows" {
//    Properties {
//     _ShadowMapTexture ("", any) = "" {}
//    }
//    Fallback Off
//    SubShader 1/1 {
//      Pass 1/1 {
//        ZTest Always
//        ZWrite Off
//        Cull Off
//        Fog { Mode Off }
//        Program "fp" {
//          SubProgram "d3d9 " {
//            Keywords { "SHADOWS_NONATIVE" "SHADOWS_SPLIT_SPHERES" }
// ----- These inputs did not change -----
//            SetTexture 0 [_CameraDepthTexture] 2D 0
//            SetTexture 1 [_ShadowMapTexture] 2D 1
// ----- These are inputs to the Dreamfall shader -----
//           Matrix 0 [_CameraToWorld]
//           Vector 4 [_ZBufferParams]
//           Vector 5 [unity_ShadowSplitSpheres0]
//           Vector 6 [unity_ShadowSplitSpheres1]
//           Vector 7 [unity_ShadowSplitSpheres2]
//           Vector 8 [unity_ShadowSplitSpheres3]
//           Vector 9 [unity_ShadowSplitSqRadii]
//           Vector 10 [unity_LightShadowBias]
//           Vector 11 [_LightShadowData]
//           Vector 12 [_ShadowMapTexture_TexelSize] | Dropped... Maybe can re-implement?
// ----- These are the inputs to the ***VERTEX*** shader - we can use them IF they wern't defined above ------
//       ---- Matrix 0 [glstate_matrix_mvp]
//       ---- Matrix 4 [unity_World2Shadow0]
//       ---- Matrix 8 [unity_World2Shadow1]
//       -+++ Matrix 12 [unity_World2Shadow2]
//       ++++ Matrix 16 [unity_World2Shadow3]
//       ++++ Matrix 20 [_CameraToWorld]
// ----- These are the inputs to the original shader from Unity -----
//            Matrix 0 [unity_World2Shadow0]          | Replace c0-c3 with c100-103 (From Helix Mod)
//            Matrix 4 [unity_World2Shadow1]          | Replace c4 with texcoord8, c5-6 with c105-106 (From Helix Mod), drop c7
//            Matrix 8 [unity_World2Shadow2]          | Replace c8-10 with texcoord2-4, drop c11
//            Matrix 12 [unity_World2Shadow3]         | Replace c12-14 with texcoord5-7, drop c15
//            Matrix 16 [_CameraToWorld]              | Replace c16-c19 with c0-c3
//            Vector 20 [_ZBufferParams]              | Replace c20 with c4
//            Vector 21 [unity_ShadowSplitSpheres0]   | Replace c21 with c5
//            Vector 22 [unity_ShadowSplitSpheres1]   | Replace c22 with c6
//            Vector 23 [unity_ShadowSplitSpheres2]   | Replace c23 with c7
//            Vector 24 [unity_ShadowSplitSpheres3]   | Replace c24 with c8
//            Vector 25 [unity_ShadowSplitSqRadii]    | Replace c25 with c9
//            Vector 26 [_LightShadowData]            | Replace c26 with c11
//          }
//        }
//      }
//    }
//  }
//  
//  Headers extracted with DarkStarSword's extract_unity_shaders.py
//  https://raw.githubusercontent.com/DarkStarSword/3d-fixes/master/extract_unity_shaders.py

    ps_3_0
    dcl_2d s0
    dcl_2d s1
    def c27, 1.00000000, 255.00000000, 0.00392157, 0.00000000
    dcl_texcoord v0.xy // texcoord (uv?)
    dcl_texcoord1 v1 //.xyz = v.normal (ray?), .w = P.I[0,0]

// Extra inputs passed from VS:
dcl_texcoord2 v2 // unity_World2Shadow2[0]
dcl_texcoord3 v3 // unity_World2Shadow2[1]
dcl_texcoord4 v4 // unity_World2Shadow2[2]
dcl_texcoord5 v5 // unity_World2Shadow3[0]
dcl_texcoord6 v6 // unity_World2Shadow3[1]
dcl_texcoord7 v7 // unity_World2Shadow3[2]
dcl_texcoord8 v8 // unity_World2Shadow1[0]

def c220, 0, 0.85, 0.0625, 0.5
dcl_2d s13

    texld r0, v0, s0
    mad r0.x, r0, c4, c4.y // _ZBufferParams
    rcp r0.x, r0.x
    mul r2.xyz, r0.x, v1
    mov r2.w, c27.x

// PRE-EMPTIVE FIX! THIS HAS NOT BEEN VERIFIED!

// This is a variation of the stereo-correction formula for view-space coords:
texldl r31, c220.z, s13 // r31.x = separation, r31.y = convergence
add r31.w, r2.z, -r31.y // r31.w = depth - convergence
mul r31.w, r31.w, r31.x // r31.w = separation * (depth - convergence)
mad r2.x, -r31.w, v1.w, r2.x // X -= separation * (depth - convergence) * P.I[0,0]

    dp4 r3.z, r2, c2 // _CameraToWorld[2]
    dp4 r3.x, r2, c0 // _CameraToWorld[0]
    dp4 r3.y, r2, c1 // _CameraToWorld[1]
    dp4 r3.w, r2, c3 // _CameraToWorld[3]
    add r1.xyz, r3, -c5 // unity_ShadowSplitSpheres0
    add r5.xyz, r3, -c8 // unity_ShadowSplitSpheres3
    dp3 r1.x, r1, r1
    add r4.xyz, r3, -c6 // unity_ShadowSplitSpheres1
    dp3 r1.y, r4, r4
    add r4.xyz, r3, -c7 // unity_ShadowSplitSpheres2
    dp3 r1.z, r4, r4
    dp3 r1.w, r5, r5
    add r1, r1, -c9 // unity_ShadowSplitSqRadii
    cmp r1, r1, c27.w, c27.x
    mov r4.x, r1.y
    mov r4.y, r1.z
    mov r4.z, r1.w
    add_sat r5.xyz, r4, -r1
    dp4 r2.z, r3, c106 // unity_World2Shadow1[2]
    dp4 r2.x, r3, v8 // unity_World2Shadow1[0]
    dp4 r2.y, r3, c105 // unity_World2Shadow1[1]
    mul r4.xyz, r5.x, r2
    dp4 r2.z, r3, c102 // unity_World2Shadow0[2]
    dp4 r2.y, r3, c101 // unity_World2Shadow0[1]
    dp4 r2.x, r3, c100 // unity_World2Shadow0[0]
    mad r2.xyz, r1.x, r2, r4
    dp4 r1.z, r3, v4 // unity_World2Shadow2[2]
    dp4 r1.y, r3, v3 // unity_World2Shadow2[1]
    dp4 r1.x, r3, v2 // unity_World2Shadow2[0]
    mad r1.xyz, r5.y, r1, r2
    dp4 r2.z, r3, v7 // unity_World2Shadow3[2]
    dp4 r2.y, r3, v6 // unity_World2Shadow3[1]
    dp4 r2.x, r3, v5 // unity_World2Shadow3[0]
    mad r1.xyz, r2, r5.z, r1
    mov r0.y, c27.x
    texld r2, r1, s1
    add r1.x, -r0, c27
    add r0.x, r2, -r1.z
    mul r2.xy, r1.x, c27
    mov r1.x, c11 // _LightShadowData
    frc r2.xy, r2
    cmp r0.x, r0, c27, r1
    mov r1.y, r2
    mad r1.x, -r2.y, c27.z, r2
    mov r0.w, r1.y
    mov r0.z, r1.x
    mov_pp oC0, r0
